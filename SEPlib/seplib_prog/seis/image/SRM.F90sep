!!$
!!$=head1 NAME
!!$
!!$ SRM - Stolt Residual Migration
!!$
!!$=head1 SYNOPSIS
!!$
!!$SRM par=parfile.P < input.H > output.H
!!$
!!$=head1 SCAN PARAMETERS
!!$
!!$=over 4
!!$
!!$=item or=1. - (real)
!!$
!!$ first residual slowness to scan over
!!$
!!$=item dr=1. - (real)
!!$
!!$ sampling of the residual slowness to scan over
!!$
!!$=item nr - (integer)
!!$
!!$ number of residual slowness to scan over
!!$
!!$=back
!!$
!!$=head1 STRETCH PARAMETERS
!!$
!!$=over 4
!!$
!!$
!!$=item push=.true. - (logical)
!!$
!!$ whether to do a push or pull operation in stretch
!!$
!!$=item scale=.false. - (logical)
!!$
!!$ whether to scale
!!$
!!$=item eps=.5 - (real)
!!$
!!$ fitting parameter for push/pull operation
!!$
!!$=back
!!$
!!$=head1 RUN PARAMETERS
!!$
!!$=over 4
!!$
!!$=item maxmem=100. - (real)
!!$
!!$ memory to use in MB
!!$
!!$=item verb=.false. - (logical)
!!$
!!$ verbosity
!!$
!!$=back
!!$
!!$=head1 DESCRIPTION
!!$
!!$Perform Stolt residual migration (See Paul Sava SEP-100,SEP-102)
!!$
!!$=head1 CATEGORY
!!$
!!$B<seis/image>
!!$
!!$=cut
!!$      
program SRM
  !! Stolt Residual Migration
  !! Paul Sava (paul@sep.stanford.edu)
  !! 12/1/00

  !! complex input/output
  !! input  is kz-khx-khy-kmx-kmy
  !! output is kz-khx-khy-kmx-kmy-ro

  use sep
  use utilities_mod
  use storm_mod

  integer :: ierr
  integer, parameter:: dbl=selected_int_kind(15)

  type(paxis) :: az,ahx,ahy,amx,amy,ar, aj

#ifdef SEP_OMP
  integer, external :: omp_get_num_threads
  integer, external :: omp_get_thread_num
#endif

  logical     :: verb, push, scale
  real        :: ro, eps
  integer     :: stat

  !! z-hx-hy-ngathers
  complex, dimension(:,:,:,:), pointer :: data

  integer :: imx,imy,ir

  real    :: maxmem    !! max memory available  (Mb)
  real    :: gathermem !! memory for one gather (Mb)
  integer :: nth,      ith,esize
  integer :: nblocks,  iblocks
  integer(kind=dbl) :: ngathers, igathers, allgathers

  call sep_init('')
  call SEP_ADD_DOC() 

  call pinaxis(az ,1); az%l ="z";  call pouaxis(az ,1)
  call pinaxis(ahx,2); ahx%l="hx"; call pouaxis(ahx,2)
  call pinaxis(ahy,3); ahy%l="hy"; call pouaxis(ahy,3)
  call pinaxis(amx,4); amx%l="mx"; call pouaxis(amx,4)
  call pinaxis(amy,5); amy%l="my"; call pouaxis(amy,5)

  call from_param("or",ar%o,1.0)
  call from_param("dr",ar%d,0.01) 
  call from_param("nr",ar%n,1)
  ar%l="ro"
  call pouaxis(ar,6)

  aj%n=1; aj%o=0.; aj%d=1.; aj%l=" "
  call pouaxis(aj,7)

  call from_param("maxmem",maxmem,100.)
  call from_param("verb",verb,.false.)
  call from_param("push",push,.true.)
  call from_param("scale",scale,.false.)
  call from_param("eps",eps,0.5)
  call from_history("esize",esize,4)
  if(esize/=8) call seperr("expecting complex data")

  call sep_close()

  !----------------------------------------------------------------
  !! find out how many threads the environment variable actually allows
#ifdef SEP_OMP
  !$OMP PARALLEL
  nth = omp_get_num_threads()
  !$OMP END PARALLEL
#else
  nth=1
#endif

  !! total # of gathers
  allgathers = amy%n * amx%n

  !! memory needed for one gather
  gathermem = (1/1024./1024.) * az%n * ahx%n * ahy%n * 8 
  !! z*hx*hy*(4+8) (Mb)

  !! find the max # of gathers that fit in the allocated memory
  ngathers = maxmem/gathermem

  !! find, if possible, the max value of ngathers 
  !!       which divides allgathers
  !!      and is divided by nth

  if(nth>allgathers) nth=allgathers
    do while(allgathers/nth *nth /= allgathers)
       nth=nth-1
    end do

    do i=1,allgathers
       if(allgathers/i  *i   ==allgathers .and. &
       i/nth*nth ==i    .and. &
       i <= ngathers ) k=i
    end do
    ngathers = k

#ifdef SEP_OMP
  !! set and freeze the # of threads
  call omp_set_dynamic(.false.)
  call omp_set_num_threads(nth)
#endif

  !! # of data blocks
  nblocks = allgathers/ngathers
  !----------------------------------------------------------------  

  if(verb) then
     write(0,*) "    maxmem=",maxmem,'Mb'
     write(0,*) "    effmem=",az%n*ahx%n*ahy%n*ngathers*8/1024./1024.,'Mb'
     write(0,*) "allgathers=",allgathers
     write(0,*) "  ngathers=",ngathers
     write(0,*) "       nth=",nth
     write(0,*) "   nblocks=",nblocks
  end if

  !----------------------------------------------------------------
  allocate(data(az%n,ahx%n,ahy%n,ngathers), stat=stat)
  if(stat /=0) call seperr("cant allocate data")
  data = (0.0,0.0)

  !----------------------------------------------------------------
  !! reserve enough disk space for the entire output
  if(verb) write(0,*) "reserving disk space"
  do ir=1,ar%n
     do imy=1,amy%n
        do iblocks=1,nblocks
           call sep_write(data)
        end do
     end do
  end do
  ierr= sseek('out',0,0)
  if(verb) write(0,*) "OK"

  !----------------------------------------------------------------
  call storm_init(nth,az,ahx,ahy,amx,amy,eps,push,scale)

  !----------------------------------------------------------------
  do ir=1,ar%n
     if(verb) write(0,*) ir,'/',ar%n
     ierr= sseek('in',0,0)
     ro = ar%o + (ir-1) * ar%d

     do imy=1,amy%n
        !----------------------------------------------------------------     
        do iblocks=1,nblocks
           call sep_read(data)

#ifdef SEP_OMP
           !$OMP  PARALLEL
           !$OMP  DO &
           !$OMP& SCHEDULE(DYNAMIC) &
           !$OMP& PRIVATE(ith,imx,igathers)
#endif
           do igathers=1,ngathers
              imx = igathers + (iblocks-1)*ngathers
#ifdef SEP_OMP
              ith = omp_get_thread_num()+1
#else
              ith = 1
#endif 
              call storm_srm_compute(ith,igathers,data,ro,az,ahx,ahy,imx,imy)

           end do
#ifdef SEP_OMP
           !$OMP END DO
           !$OMP END PARALLEL
#endif

           call sep_write(data)
        end do
        !----------------------------------------------------------------     
     end do

  end do
  !----------------------------------------------------------------
  deallocate(data, stat=stat)
  if(stat /=0) call seperr("cant deallocate data")

end program SRM







